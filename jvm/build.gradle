buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.17'
    }
}

apply plugin: 'kotlin-platform-jvm'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.jetbrains.dokka'

dokka {
    outputFormat = "html"
    outputDirectory = "public"
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")

    publish = true

    pkg {
        repo = 'diskord'
        name = 'diskord'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://gitlab.com/jesselcorbett/Diskord'
        publications = ['Release']
        version {
            name = diskordVersion
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }

    developers = {
        developer {
            id 'jesse corbett'
            name 'Jesse Corbett'
            email 'jesselcorbett@gmail.com'
        }
    }

    scm {
        url 'https://gitlab.com/jesselcorbett/Diskord'
    }
}

publishing {
    repositories {
        maven {
            name = "gitlab"
            url = "https://gitlab.com/api/v4/projects/${System.getenv("CI_PROJECT_ID")}/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = "Job-Token"
                value = System.getenv("CI_JOB_TOKEN")
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }

    publications {
        Release(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'diskord'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A Kotlin wrapper around the Discord API')
                root.appendNode('name', 'Diskord')
                root.appendNode('url', 'https://gitlab.com/jesselcorbett/Diskord')
                root.children().last() + pomConfig
            }
        }

        Snapshot(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'diskord'
            version "${project.version}-SNAPSHOT"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A Kotlin wrapper around the Discord API')
                root.appendNode('name', 'Diskord')
                root.appendNode('url', 'https://gitlab.com/jesselcorbett/Diskord')
                root.children().last() + pomConfig
            }
        }
    }
}

dependencies {
    expectedBy project(":")

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.8'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.8'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.8'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8'
    implementation 'org.slf4j:slf4j-api:1.7.25'

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    testImplementation 'com.willowtreeapps.assertk:assertk-jvm:0.13'
    testImplementation 'io.mockk:mockk:1.9'
}

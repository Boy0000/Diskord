
plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.11"
    id "org.jetbrains.dokka" version "0.9.17"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}

group = 'com.jessecorbett'
version = diskordVersion

dokka {
    outputFormat = "html"
    outputDirectory = "docs"
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")

    pkg {
        repo = "diskord"
        name = "diskord"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://gitlab.com/jesselcorbett/Diskord"
        publications = ['DiskordPublication']
        version {
            name = diskordVersion
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "jesse corbett"
            name "Jesse Corbett"
            email "jesselcorbett@gmail.com"
        }
    }

    scm {
        url "https://gitlab.com/jesselcorbett/Diskord"
    }
}

publishing {
    publications {
        DiskordPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'diskord'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A Kotlin wrapper around the Discord API')
                root.appendNode('name', 'Diskord')
                root.appendNode('url', 'https://gitlab.com/jesselcorbett/Diskord')
                root.children().last() + pomConfig
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0"
    implementation "com.squareup.okhttp3:okhttp:3.12.1"
    implementation "com.fasterxml.jackson.core:jackson-core:2.9.8"
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.9.8"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.8"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8"
    implementation "org.slf4j:slf4j-api:1.7.25"

    testImplementation 'junit:junit:4.12'
}
